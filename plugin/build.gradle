import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id('java')
    id("xyz.jpenilla.run-paper") version '3.0.2'
    id('com.gradleup.shadow') version '8.3.5'
}

group = 'me.tr'
version = '1.0.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    implementation(project(":core"))
}

tasks {
    runServer {
        minecraftVersion("1.21.10")
        args("--nogui")
        standardInput = System.in

        def agentJar = "C:\\Programming\\Java\\Minecraft\\Libraries\\TrItems\\hotswap-agent.jar"

        def pluginMain = project.layout.buildDirectory.dir("classes/java/main").get().asFile.absolutePath
        def coreMain = project(":core").layout.buildDirectory.dir("classes/java/main").get().asFile.absolutePath

        jvmArgs(
                "-Dhotswapagent.config=${project.projectDir}/src/main/resources/hotswap-agent.properties",
                "-XX:HotswapAgent=fatjar",
                "-XX:+AllowEnhancedClassRedefinition",
                "-javaagent:${agentJar}=autoHotswap=true",
                "--add-opens=java.base/java.lang=ALL-UNNAMED",
                "--add-opens=java.base/java.net=ALL-UNNAMED",
                "--add-opens=java.base/java.util=ALL-UNNAMED",
                "-Dextra.classpath=${coreMain};${pluginMain}"
        )
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(ShadowJar).configureEach {
    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude 'META-INF/maven/**'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

tasks.assemble {
    dependsOn(tasks.shadowJar)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"
}